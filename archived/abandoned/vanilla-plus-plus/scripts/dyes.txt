# Makes dyes use tags instead of having a ton of duplicate recipes
# I don't even use them, but it still bothers me lol
print("dyes.zs loading...");

/*
craftingTable.remove(<item>);
craftingTable.removeByName("name");

craftingTable.addShapeless("recipeName", <output>, [<inputs>]);
*/

val white = <item:minecraft:white_dye>;
val orange = <item:minecraft:orange_dye>;
val magenta = <item:minecraft:magenta_dye>;
val light_blue = <item:minecraft:light_blue_dye>;
val yellow = <item:minecraft:yellow_dye>;
# val lime = <item:minecraft:lime_dye>;
val pink = <item:minecraft:pink_dye>;
# val gray = <item:minecraft:gray_dye>;
val light_gray = <item:minecraft:light_gray_dye>;
val cyan = <item:minecraft:cyan_dye>;
val purple = <item:minecraft:purple_dye>;
val blue = <item:minecraft:blue_dye>;
val brown = <item:minecraft:brown_dye>;
val green = <item:minecraft:green_dye>;
val red = <item:minecraft:red_dye>;
val black = <item:minecraft:black_dye>;



# White
craftingTable.remove(white);
craftingTable.addShapeless("white_dye", white, [<tag:items:modpack:white_dye_materials>]);


# Orange
craftingTable.remove(orange);
craftingTable.addShapeless("orange_dye", orange, [<tag:items:modpack:orange_dye_materials>]);
craftingTable.addShapeless("orange_dye_combine", orange*2, [<tag:items:forge:dyes/red>, <tag:items:forge:dyes/yellow>]);


# Magenta
craftingTable.remove(magenta);
craftingTable.addShapeless("magenta_dye", magenta, [<tag:items:modpack:magenta_dye_materials>]);
craftingTable.addShapeless("magenta_dye_double", magenta*2, [<tag:items:modpack:magenta_dye_materials_double>]);
craftingTable.addShapeless("magenta_dye_combine", magenta*2, [<tag:items:forge:dyes/purple>, <tag:items:forge:dyes/pink>]);
craftingTable.addShapeless("magenta_dye_triple_combine", magenta*3, [<tag:items:forge:dyes/red>, <tag:items:forge:dyes/blue>, <tag:items:forge:dyes/pink>]);
craftingTable.addShapeless("magenta_dye_quad_combine", magenta*4, [<tag:items:forge:dyes/red>, <tag:items:forge:dyes/red>, <tag:items:forge:dyes/blue>, <tag:items:forge:dyes/white>]);


# Light Blue
craftingTable.remove(light_blue);
craftingTable.addShapeless("light_blue_dye", light_blue, [<tag:items:modpack:light_blue_dye_materials>]);
craftingTable.addShapeless("light_blue_dye_double", light_blue*2, [<tag:items:modpack:light_blue_dye_materials_double>]);
craftingTable.addShapeless("light_blue_dye_combine", light_blue*2, [<tag:items:forge:dyes/white>, <tag:items:forge:dyes/blue>]);


# Yellow
craftingTable.remove(yellow);
craftingTable.addShapeless("yellow_dye", yellow, [<tag:items:modpack:yellow_dye_materials>]);
craftingTable.addShapeless("yellow_dye_double", yellow*2, [<tag:items:modpack:yellow_dye_materials_double>]);

# Lime has no recipes

# Pink
craftingTable.remove(pink);
craftingTable.addShapeless("pink_dye", pink, [<tag:items:modpack:pink_dye_materials>]);
craftingTable.addShapeless("pink_dye_double", pink*2, [<tag:items:modpack:pink_dye_materials_double>]);
craftingTable.addShapeless("pink_dye_combine", pink*2, [<tag:items:forge:dyes/white>, <tag:items:forge:dyes/red>]);


# Gray only has 1 recipe
/*
craftingTable.remove(gray);
craftingTable.addShapeless("gray_dye", gray, [<tag:items:modpack:gray_dye_materials>]);
craftingTable.addShapeless("gray_dye_combine", gray*2, [<tag:items:forge:dyes/white>, <tag:items:forge:dyes/black>]);
*/

# Light Gray
craftingTable.remove(light_gray);
craftingTable.addShapeless("light_gray_dye", light_gray, [<tag:items:modpack:light_gray_dye_materials>]);
craftingTable.addShapeless("light_gray_dye_combine", light_gray*2, [<tag:items:forge:dyes/white>, <tag:items:forge:dyes/gray>]);
craftingTable.addShapeless("light_gray_dye_triple_combine", light_gray*3, [<tag:items:forge:dyes/white>, <tag:items:forge:dyes/white>, <tag:items:forge:dyes/black>]);


# Cyan
craftingTable.remove(cyan);
craftingTable.addShapeless("cyan_dye", cyan, [<tag:items:modpack:cyan_dye_materials>]);
craftingTable.addShapeless("cyan_dye_combine", cyan*2, [<tag:items:forge:dyes/green>, <tag:items:forge:dyes/blue>]);
furnace.addRecipe("fungus_cyan", cyan, <item:minecraft:warped_fungus>, 1, 200);


# Purple
craftingTable.remove(purple);
craftingTable.addShapeless("purple_dye", purple, [<tag:items:modpack:purple_dye_materials>]);
craftingTable.addShapeless("purple_dye_double", purple, [<tag:items:modpack:purple_dye_materials_double>]);
craftingTable.addShapeless("purple_dye_combine", purple*2, [<tag:items:forge:dyes/red>, <tag:items:forge:dyes/blue>]);


# Blue
craftingTable.remove(blue);
craftingTable.addShapeless("blue_dye", blue, [<tag:items:modpack:blue_dye_materials>]);


# Brown
craftingTable.remove(brown);
craftingTable.addShapeless("brown_dye", brown, [<tag:items:modpack:brown_dye_materials>]);
craftingTable.addShapeless("brown_dye_double", brown*2, [<tag:items:modpack:brown_dye_materials_double>]);


# Green
craftingTable.remove(green);
craftingTable.addShapeless("green_dye", green, [<tag:items:modpack:green_dye_materials>]);
craftingTable.addShapeless("green_dye_double", green, [<tag:items:modpack:green_dye_materials_double>]);
furnace.addRecipe("dripleaf_green", green, <item:minecraft:small_dripleaf>|<item:minecraft:big_dripleaf>, 1, 100);
furnace.addRecipe("scute_green", green, <item:minecraft:scute>, 1, 200);


# Red
craftingTable.remove(red);
craftingTable.addShapeless("red_dye", red, [<tag:items:modpack:red_dye_materials>]);
craftingTable.addShapeless("red_dye_double", red*2, [<tag:items:modpack:red_dye_materials_double>]);
furnace.addRecipe("fungus_red", red, <item:minecraft:crimson_fungus>, 1, 100);


# Black
craftingTable.remove(black);
craftingTable.addShapeless("black_dye", black, [<tag:items:modpack:black_dye_materials>]);
craftingTable.addShapeless("quadruple_black_dye", black*4, [<tag:items:modpack:black_dye_materials_quadruple>]);

print("dyes.zs loaded");